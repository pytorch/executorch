/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree.
 */

#ifndef LINEAR_FP_OUTPUT_TILE_FP_INT8_COMPUTE_GLSLH
#define LINEAR_FP_OUTPUT_TILE_FP_INT8_COMPUTE_GLSLH

#extension GL_EXT_control_flow_attributes : require

#include "linear_common.glslh"
#include "linear_fp_input_tile.glslh"
#include "linear_fp_output_tile.glslh"
#include "linear_int8_weight_tile.glslh"

VEC4_T unpack_packed_4xint8(int int8x4) {
  return VEC4_T(
      extract_8bit_from_packed_int_le(int8x4, 0),
      extract_8bit_from_packed_int_le(int8x4, 1),
      extract_8bit_from_packed_int_le(int8x4, 2),
      extract_8bit_from_packed_int_le(int8x4, 3));
}

void fp_accumulate_with_int8_weight(
    inout FPOutTile accum,
    FPInputTile in_tile,
    Int8WeightTile w_tile) {
  // Accum tile is indexed as accum[m][n4][n4i]
  //   -> gives fp accumulator for output tile element at (x = n, y = m)
  // Input tile is indexed as in_tile.data[m][k4]
  //   -> gives vec4 containing the fp inputs at index
  //      (k, m), (k + 1, m), (k + 2, m), (k + 3, m)
  // Weight tile is indexed as w_tile.data[k4][n4][n4i]
  //   -> gives packed integer containing the 4x 8-bit quantized values at index
  //      (n, k), (n, k + 1), (n, k + 2), (n, k + 3)
  VEC4_T weight_texel;
  [[unroll]] for (int n4 = 0; n4 < TILE_N4; ++n4) {
    [[unroll]] for (int k4 = 0; k4 < TILE_K4; ++k4) {
      [[unroll]] for (int k4i = 0; k4i < 4; ++k4i) {
        // Unpack one column of weights
        weight_texel = VEC4_T(
            extract_8bit_from_packed_int_le(w_tile.data[k4][n4][0], k4i),
            extract_8bit_from_packed_int_le(w_tile.data[k4][n4][1], k4i),
            extract_8bit_from_packed_int_le(w_tile.data[k4][n4][2], k4i),
            extract_8bit_from_packed_int_le(w_tile.data[k4][n4][3], k4i));

        for (int m = 0; m < TILE_M; ++m) {
          accum.data[m][n4] =
              fma(VEC4_T(in_tile.data[m][k4][k4i]),
                  weight_texel,
                  accum.data[m][n4]);
        }
      }
    }
  }
}

#endif // LINEAR_FP_OUTPUT_TILE_FP_INT8_COMPUTE_GLSLH
