/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree.
 */

#ifndef LINEAR_INT8_WEIGHT_BLOCK_GLSLH
#define LINEAR_INT8_WEIGHT_BLOCK_GLSLH

/*
 * Assumes the following variables are defined in the shader layout
 * - t_packed_int8_weight
 * - t_int8_weight
 *
 * Macro Settings:
 * - USING_BUFFER
 */

#extension GL_EXT_control_flow_attributes : require

#include "linear_common.glslh"

// Represents data for a 4x4 block of the weight matrix read from the input
// buffer.
struct Int8WeightBlock {
  ivec4 data;
};

void load_block_data_no_checks(
    out Int8WeightBlock block,
    const int k4,
    const int n_start,
    const int ntexels_K,
    const int N) {
  [[unroll]] for (int n = 0; n < 4; ++n) {
    block.data[n] = t_int8_weight[(n_start + n) * ntexels_K + k4];
  }
}

void load_block_data_with_checks(
    out Int8WeightBlock block,
    const int k4,
    const int n_start,
    const int ntexels_K,
    const int N) {
  [[unroll]] for (int n = 0; n < 4; ++n) {
    if (n_start + n < N) {
      block.data[n] = t_int8_weight[(n_start + n) * ntexels_K + k4];
    } else {
      block.data[n] = 0;
    }
  }
}

void write_weight_block(
    const Int8WeightBlock block,
    const int n4,
    const int k4,
    const int ntexels_N) {
#ifdef USING_BUFFER
  t_packed_int8_weight[k4 * ntexels_N + n4] = block.data;
#else
  imageStore(t_packed_int8_weight, ivec2(n4, k4), block.data);
#endif
}

#ifdef DEBUG_MODE

void printInt8WeightBlock(const Int8WeightBlockPacked block) {
  debugPrintfEXT("int8_weight_block_packed: \\n");
  debugPrintfEXT(
      "%i %i %i %i \\n",
      block.data[0],
      block.data[1],
      block.data[2],
      block.data[3]);
}

#endif // DEBUG_MODE

#endif // LINEAR_INT8_WEIGHT_BLOCK_GLSLH
